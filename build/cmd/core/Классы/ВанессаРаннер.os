// BSLLS:MissingVariablesDescription-off

#Использовать 1commands

#Область ОписанияПеременных

Перем ПараметрыКоманды;
Перем ПеременныеСредыКоманды;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура УстановитьСтрокуСоединения(СтрокаСоединения = Неопределено) Экспорт
	ПараметрыКоманды.Вставить("--ibconnection", СтрокаСоединения);
КонецПроцедуры

Процедура УстановитьПользователяИБ(ПользовательИБ) Экспорт
	ПеременныеСредыКоманды.Вставить("RUNNER_DBUSER", ПользовательИБ);
КонецПроцедуры

Процедура УстановитьПарольПользователяИБ(ПарольПользователяИБ) Экспорт
	ПеременныеСредыКоманды.Вставить("RUNNER_DBPWD", ПарольПользователяИБ);
КонецПроцедуры

Процедура УстановитьВерсиюПлатформы(ВерсияПлатформы = Неопределено) Экспорт
	ПараметрыКоманды.Вставить("--v8version", ВерсияПлатформы);
КонецПроцедуры

Процедура УстановитьПараметрЗапуска(ПараметрыЗапуска) Экспорт
	
	Если ТипЗнч(ПараметрыЗапуска) = Тип("Структура") Тогда
		
		СписокПараметровЗапуска = Новый Массив;
		
		ШаблонПараметра = "%1=%2";
		Для Каждого КлючИЗначение Из ПараметрыЗапуска Цикл
			
			Если КлючИЗначение.Значение = Неопределено Тогда
				СписокПараметровЗапуска.Добавить(КлючИЗначение.Ключ);
			Иначе
				ТекстПараметра = СтрШаблон(ШаблонПараметра, КлючИЗначение.Ключ, КлючИЗначение.Значение);
				СписокПараметровЗапуска.Добавить(ТекстПараметра);
			КонецЕсли;
			
		КонеЦЦикла;
		
		ПараметрЗапуска = СтрСоединить(СписокПараметровЗапуска, ";");
		
	ИначеЕсли ТипЗнч(ПараметрыЗапуска) = Тип("Массив") Тогда
		ПараметрЗапуска = СтрСоединить(ПараметрыЗапуска, ";");
		
	Иначе
		ПараметрЗапуска = ПараметрыЗапуска;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрЗапуска) Тогда
		ПараметрыКоманды.Вставить("--command", СтрШаблон("""%1""", ПараметрЗапуска));
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьДополнительныеПараметры(ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры) Тогда
		ПараметрыКоманды.Вставить("--additional", СтрШаблон("""%1""", ДополнительныеПараметры));
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПроизвольныйПараметр(ИмяПараметра, ЗначениеПараметра = Неопределено) Экспорт
	ПараметрыКоманды.Вставить(ИмяПараметра, ЗначениеПараметра);
КонецПроцедуры

Функция Запустить(ИмяКоманды) Экспорт
	
	ПодготовленныеПараметры = Новый Массив;
	
	ШаблонПараметра = "%1 %2";
	
	Для каждого КлючИЗначение Из ПараметрыКоманды Цикл
		
		ПараметрКоманды = СтрШаблон(ШаблонПараметра, КлючИЗначение.Ключ, КлючИЗначение.Значение);
		ПодготовленныеПараметры.Добавить(ПараметрКоманды);
		
	КонецЦикла;
	
	ПараметрыКоманднойСтроки = СтрСоединить(ПодготовленныеПараметры, " ");
	КоманднаяСтрока = СтрШаблон("vrunner %1 %2", ИмяКоманды, ПараметрыКоманднойСтроки);
	
	Команда = Новый Команда;
	Команда.ПоказыватьВыводНемедленно(Истина);
	Команда.УстановитьСтрокуЗапуска(КоманднаяСтрока);
	Команда.УстановитьПеременныеСреды(ПеременныеСредыКоманды);
	
	Сообщить(КоманднаяСтрока);
	Возврат Команда.Исполнить();
	
КонецФункции

Процедура ПроверитьКодВозврата(КодВозврата) Экспорт
	
	Если КодВозврата <> 0 Тогда
		ВызватьИсключение СтрШаблон("Выполнение команды VRunner завершилось с ошибкой: %1", КодВозврата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта() Экспорт
	ПараметрыКоманды = Новый Соответствие;
	ПеременныеСредыКоманды = Новый Соответствие;
КонецПроцедуры

#КонецОбласти
