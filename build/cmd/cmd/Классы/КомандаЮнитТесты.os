// BSLLS:MissingVariablesDescription-off

#Использовать "..\..\core"

#Область ОписанияПеременных

Перем СтрокаСоединения;
Перем ВерсияПлатформы;
Перем ИмяФайлаПокрытия;
Перем АдресОтладчика;
Перем ИмяИнформационнойБазыОтладки;

#КонецОбласти

#Область КомандаПриложения

Процедура ОписаниеКоманды(Команда) Экспорт
	
	ПараметрыПроекта.ДобавитьОпцииСоединенияИБ(Команда);
	ПараметрыПроекта.ДобавитьОпцииПокрытия(Команда);
	
КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	Сообщить("Запускаем тесты");
	
	СтрокаСоединения = Команда.ЗначениеОпции("ibconnection");
	ВерсияПлатформы = Команда.ЗначениеОпции("v8version");
	ИмяФайлаПокрытия = Команда.ЗначениеОпции("coverage-path");
	АдресОтладчика = Команда.ЗначениеОпции("debugger-url");
	ИмяИнформационнойБазыОтладки = Команда.ЗначениеОпции("debugger-alias");
	
	Если ПустаяСтрока(ИмяФайлаПокрытия) Тогда
		
		ЗапускТестов();
		
	Иначе
		
		Действие = Новый Действие(ЭтотОбъект, "ЗапускТестов");
		ДанныеПокрытия = Новый Покрытие()
			.УстановитьАдресОтладчика(АдресОтладчика)
			.УстановитьИмяИнформационнойБазы(ИмяИнформационнойБазыОтладки)
			.УстановитьВерсиюПлатформы(ВерсияПлатформы)
			.ВыполнитьСбор(Действие);
		
		ПараметрыПроекта.ЗаписатьДанныеJSON(ДанныеПокрытия, ИмяФайлаПокрытия);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗапускТестов(ПараметрыОтладки = "") Экспорт
	
	Раннер = Новый ВанессаРаннер;
	Раннер.УстановитьПроизвольныйПараметр("--config-tests");
	Раннер.УстановитьПроизвольныйПараметр("--settings", "build/vrunnerXUnit.json");
	Раннер.УстановитьДополнительныеПараметры(ПараметрыОтладки);
	
	Раннер.Запустить("xunit");	

КонецПроцедуры

#КонецОбласти
